buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath 'org.comroid:japi:+'
    }
}

apply plugin: 'groovy-gradle-plugin'
apply plugin: 'maven-publish'

group = 'com.ampznetwork.modsuite'
version = 1

gradlePlugin.plugins {
    register('buildscripts') {
        id = 'com.ampznetwork.modsuite.buildscripts'
        displayName = 'AMPZ Buildscripts'
        description = 'Helper Scripts for building Minecraft Plugins and Mods'
        implementationClass = 'com.ampznetwork.modsuite.buildscripts.ModSuiteBuildHelper'
    }
    register('spigotResourceGenerator') {
        id = 'org.comroid.codegen.spigot'
        displayName = 'Spigot Plugin Code Generation'
        tags.set(['codegen', 'spigot'])
        implementationClass = 'org.comroid.codegen.spigot.SpigotResourceGeneratorPlugin'
    }
}

sourceSets {
    gradle {
        groovy {
            srcDirs += files("$projectDir/scripts")
        }
    }
}

var task = tasks.findByName 'compileGradleGroovy'
if (task != null)
    task.enabled = false

repositories {
    mavenLocal()
    maven {
        name = 'comroid'
        url = 'https://maven.comroid.org'
    }
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
    gradlePluginPortal()
    mavenCentral()
}

dependencies {
    implementation 'org.comroid:japi:+'
    implementation 'org.jetbrains:annotations:24.+'
    implementation 'org.yaml:snakeyaml:2.+'
    implementation 'de.undercouch:gradle-download-task:5.5.+'
    implementation 'org.projectlombok:lombok:+'
    annotationProcessor 'org.projectlombok:lombok:+'
}

jar.finalizedBy publishToMavenLocal
